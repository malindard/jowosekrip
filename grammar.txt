statements  : NEWLINE* statement (NEWLINE+ statement)* NEWLINE*

statement		: KEYWORD:BALIK expr?
						: KEYWORD:TERUSAKE
						: KEYWORD:MANDHEG
						: expr

expr        : KEYWORD:VAR IDENTIFIER EQ expr
            : comp-expr ((KEYWORD:LAN|KEYWORD:UTAWA) comp-expr)*

comp-expr   : ORA comp-expr
            : arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr  :	term ((PLUS|MINUS) term)*

term        : factor ((MUL|DIV) factor)*

factor      : (PLUS|MINUS) factor
            : power

power       : call (POW factor)*

call        : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom        : INT|FLOAT|STRING|IDENTIFIER
            : LPAREN expr RPAREN
            : list-expr
            : if-expr
            : for-expr
            : while-expr
            : func-def

list-expr   : LSQUARE (expr (COMMA expr)*)? RSQUARE

if-expr     : KEYWORD:YEN expr KEYWORD:BANJUR
              (statement if-expr-b|if-expr-c?)
            | (NEWLINE statements KEYWORD:RAMPUNG|if-expr-b|if-expr-c)

if-expr-b   : KEYWORD:LIYANEYEN expr KEYWORD:BANJUR
              (statement if-expr-b|if-expr-c?)
            | (NEWLINE statements KEYWORD:RAMPUNG|if-expr-b|if-expr-c)

if-expr-c   : KEYWORD:LIYANE
              statement
            | (NEWLINE statements KEYWORD:RAMPUNG)

for-expr    : KEYWORD:KANGGO IDENTIFIER EQ expr KEYWORD:MENYANG expr 
              (KEYWORD:JANGKAH expr)? KEYWORD:BANJUR
              statement
            | (NEWLINE statements KEYWORD:RAMPUNG)

while-expr  : KEYWORD:NALIKA expr KEYWORD:BANJUR
              statement
            | (NEWLINE statements KEYWORD:RAMPUNG)

func-def    : KEYWORD:FUN IDENTIFIER?
              LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
              (ARROW expr)
            | (NEWLINE statements KEYWORD:RAMPUNG)